set logging file gdb.txt
set logging on

# allow background execution
set target-async on

# pagination needs to be off for the following setting
set pagination off

# allow some threads to run while others are stopped
set non-stop on
set detach-on-fork off

#Set paths
set environment DBG_IMG_SUBDIR=brs2s_dbg_6
set environment DBG_FAMILY_SUBDIR=DEBUG_6
set environment TOOLCHAIN=/media/SSD/tools/gcc-linaro-6.4.1-2017.08-x86_64_arm-linux-gnueabihf

# se executa din folderul in care se afla git-urile
set sysroot p5_smart_shine/target/$DBG_IMG_SUBDIR/rootfs.unstripped/

set solib-absolute-prefix p5_smart_shine/target/$DBG_IMG_SUBDIR/rootfs.unstripped/

set solib-search-path p5_smart_shine/target/$DBG_IMG_SUBDIR/rootfs.unstripped/:${TOOLCHAIN}/arm-linux-gnueabihf/lib:${TOOLCHAIN}/arm-linux-gnueabihf/libc/usr/lib

set debug-file-directory $TOOLCHAIN/arm-linux-gnueabihf/libc/lib/debug


#Correctly show source files for stuff
directory ./p5_hirschmann_shared/
directory ./p5_smart_shine/
directory ./p5_linux_kernel/busybox/
directory ./p5_linux_kernel/kernel/linux-4.4.y-ti/

#Also show GLIBC source code, in case it is downloaded - see the toolchain for the exact repo and revision
#directory ../TOOLCHAINS/glibc/manual/
#directory ../TOOLCHAINS/glibc/nptl/


define brs_switchdrv
dont-repeat
file p5_smart_shine/target/am335_l/avenger/DEBUG_6/ipl/switchdrvr
target extended-remote 192.168.248.100:12345

python
p = gdb.execute('info os processes', to_string=True)
pr = p.split("\n")
for line in pr:
    if "switchdrvr" in line:
        tmp = line.strip().split(" ")
        pid = int(tmp[0])
        if pid < 1000:
            gdb.write("Attaching to " + tmp[0] + "\n")
            gdb.execute('attach ' + tmp[0], True)
end
end

brs_switchdrv

